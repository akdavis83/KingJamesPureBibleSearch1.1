INCLUDEPATH += $$[QT_INSTALL_HEADERS]/wwWidgets

#qtAddLibrary(wwwidgets4)
mac {
	static {
		iphonesimulator {
			!declarative_debug:LIBS += -lwwwidgets4_iphonesimulator
			declarative_debug:LIBS += -lwwwidgets4_iphonesimulator_debug
		} else {
			!declarative_debug:LIBS += -lwwwidgets4
			declarative_debug:LIBS += -lwwwidgets4_debug
		}
	} else {
		greaterThan(QT_MAJOR_VERSION, 4) {
			QMAKE_LFLAGS += -F$$[QT_INSTALL_LIBS]
			LIBS += -framework wwwidgets4
		} else {
			!declarative_debug:LIBS += -L$$[QT_INSTALL_LIBS] -lwwwidgets4
			declarative_debug:LIBS += -L$$[QT_INSTALL_LIBS] -lwwwidgets4_debug
		}
	}
} else {
	win32 {
		debug:LIBS += -lwwwidgets4d
		release:LIBS += -lwwwidgets4
	} else {
		CONFIG(release, debug|release) {
			release:LIBS += -L$$[QT_INSTALL_LIBS] -lwwwidgets4
		} else {
			debug:LIBS += -L$$[QT_INSTALL_LIBS] -lwwwidgets4d
		}
	}
}

DEFINES += WWWIDGETS
DEFINES -= NO_WWWIDGETS
WW_VERSION = 1.0.0
message( Welcome to wwWidgets ($$WW_VERSION) )

